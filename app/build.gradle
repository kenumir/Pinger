apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        applicationId "com.wt.pinger"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 89
        versionName "3.4.1"

        resConfigs "en", "pl"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "long", "APP_BUILD_TIMESTAMP", System.currentTimeMillis() + "L"

        Properties props = new Properties()
        props.load(new FileInputStream("local.properties"))

        buildConfigField "boolean",  "BUILD_FLAG_USE_API", "true"
        buildConfigField "String",  "API_BASE_URL", props.get('API_BASE_URL', '')
        buildConfigField "String",  "API_SECRET", props.get('API_SECRET', '')
        buildConfigField "String",  "SUPPORT_URL", props.get('SUPPORT_URL', '')
    }

    aaptOptions {
        cruncherEnabled = false
    }

    signingConfigs {
        debug {
            // ignore keystore etc
            return void
        }
        release {
            Properties props = new Properties()
            props.load(new FileInputStream("_work/sign/sing.properties"))

            storeFile file("../_work/sign/new_pinger_upload_keystore.jks")
            keyAlias props.get('SIGN_ALIAS', '')
            storePassword props.get('SING_STORE_PASS', '')
            keyPassword props.get('SIGN_ALIAS_PASS', '')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'otto.pro',
                    'retrofit.pro',
                    'okio.pro',
                    'crashlytics.pro',
                    'support_design.pro',
                    'support_v7.pro',
                    'picasso.pro',
                    'android.pro'
            signingConfig signingConfigs.release
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Pinger-3-${variant.buildType.name}-${defaultConfig.versionName}-[${defaultConfig.versionCode}].apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    def supportLibVersion = versions.librarySupport

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    implementation "com.google.firebase:firebase-analytics:${versions.libraryFirebase.analytics}"
    implementation "com.google.firebase:firebase-perf:${versions.libraryFirebase.perf}"

    implementation 'com.android.installreferrer:installreferrer:' + versions.installreferrer

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.aurelhubert:ahbottomnavigation:2.2.0'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.3.0'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.github.kenumir:EventClip:1.0.3'

    // hivedi
    implementation 'com.github.Hivedi:Console:' + versions.console
    implementation 'com.github.Hivedi:QueryBuilder:1.0.2'
    implementation 'com.github.Hivedi:ErrorReportAdapter:2.1.1'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.google.firebase.firebase-perf'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        applicationId "com.wt.pinger"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 75
        versionName "3.2.8"

        resConfigs "en", "pl"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "long", "APP_BUILD_TIMESTAMP", System.currentTimeMillis() + "L"

        Properties props = new Properties()
        props.load(new FileInputStream("local.properties"))

        buildConfigField "boolean",  "BUILD_FLAG_USE_API", "true"
        buildConfigField "String",  "API_BASE_URL", props.get('API_BASE_URL', '')
        buildConfigField "String",  "API_SECRET", props.get('API_SECRET', '')
        buildConfigField "String",  "SUPPORT_URL", props.get('SUPPORT_URL', '')
    }

    aaptOptions {
        cruncherEnabled = false
    }

    signingConfigs {
        debug {
            // ignore keystore etc
            return void
        }
        release {
            Properties props = new Properties()
            props.load(new FileInputStream("_work/sign/sing.properties"))

            storeFile file("../_work/sign/new_pinger_upload_keystore.jks")
            keyAlias props.get('SIGN_ALIAS', '')
            storePassword props.get('SING_STORE_PASS', '')
            keyPassword props.get('SIGN_ALIAS_PASS', '')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'otto.pro',
                    'retrofit.pro',
                    'okio.pro',
                    'crashlytics.pro',
                    'support_design.pro',
                    'support_v7.pro',
                    'picasso.pro',
                    'android.pro'
            signingConfig signingConfigs.release
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Pinger-3-${variant.buildType.name}-${defaultConfig.versionName}-[${defaultConfig.versionCode}].apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    def supportLibVersion = versions.librarySupport
    def fireBaseLibVersion = versions.libraryFirebase

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-compat:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:support-v13:${supportLibVersion}"

    compile "com.google.firebase:firebase-core:${fireBaseLibVersion}"
    compile "com.google.firebase:firebase-config:${fireBaseLibVersion}"
    compile "com.google.firebase:firebase-perf:${fireBaseLibVersion}"

    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') { transitive = true }
    compile('com.crashlytics.sdk.android:answers:1.4.1@aar') { transitive = true }
    compile 'com.jakewharton:butterknife:9.0.0-SNAPSHOT'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-SNAPSHOT'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.aurelhubert:ahbottomnavigation:2.1.0'
    compile 'com.github.anrwatchdog:anrwatchdog:1.3.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.afollestad.material-dialogs:core:0.9.4.7'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.github.kenumir:EventClip:1.0.3'

    // hivedi
    compile 'com.github.Hivedi:Console:' + versions.console
    compile 'com.github.Hivedi:QueryBuilder:1.0.2'
    compile 'com.github.Hivedi:ErrorReportAdapter:2.1.1'
}

apply plugin: 'com.google.gms.google-services'